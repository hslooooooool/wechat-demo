/**通用基础依赖配置表*/

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.name)
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
}

android {
    compileSdkVersion build_versions.SDK_COMPILE

    androidExtensions {
        experimental = true
    }

    defaultConfig {
        minSdkVersion build_versions.SDK_MIN
        targetSdkVersion build_versions.SDK_TARGET
        versionCode 1
        versionName "1.0.0"
        // 增加对svg的支持
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters "armeabi", 'armeabi-v7a'
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // apk 方法数超过 65536 限制，进行分包处理
        multiDexEnabled true
        dexOptions {
            javaMaxHeapSize "3g"
        }
    }

    sourceSets {
        // 数据库概要、记录存放位置
        androidTest.assets.srcDirs += files("$projectDir/schemas")

        main {
            jniLibs.srcDir(['libs'])
        }
    }

    buildTypes {
        release {
            // 开启混淆
            minifyEnabled false
            // 压缩优化
            zipAlignEnabled true
            // 默认的混淆文件以及我们指定的混淆文件，子模块使用 consumerProguardFiles
            consumerProguardFiles 'proguard-rules.pro'

            buildConfigField('String', 'BASE_URL', '"http://thoughtworks-ios.herokuapp.com/"')
        }
        debug {
            // 开启混淆
            minifyEnabled false
            // 压缩优化
            zipAlignEnabled true

            buildConfigField('String', 'BASE_URL', '"http://thoughtworks-ios.herokuapp.com/"')
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api deps.kotlin.stdlib
    kapt deps.arouter_compiler
    kapt deps.room.compiler
}

repositories {
    mavenCentral()
}

// 获取编译时间
static def apkTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

ext.time = apkTime()